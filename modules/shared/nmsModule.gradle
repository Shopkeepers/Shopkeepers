apply plugin: 'java-library'
apply plugin: 'org.checkerframework'
apply plugin: 'eclipse'

apply from: rootProject.file('modules/shared/specialSource.gradle')

// Every project using this script must declare this version BEFORE applying this script:
/*ext {
	craftbukkitVersion = 'UNSPECIFIED'
}*/

if (!project.hasProperty('craftbukkitVersion')) {
	throw new GradleException("Property 'craftbukkitVersion' must be defined before applying this script.")
}

dependencies {
	implementation project(':shopkeepers-main')
	implementation "org.bukkit:craftbukkit:${craftbukkitVersion}:remapped-mojang"

	testImplementation libs.junit
	testImplementation libs.asm
}

jar {
	doLast {
		def outputFiles = outputs.files.files
		for (inputFile in outputFiles) {
			File intermediateFile = new File(temporaryDir, inputFile.name.replace('.jar', '-obf.jar'))
			File outputFile = inputFile
			remapMojangToSpigot(inputFile, intermediateFile, outputFile, craftbukkitVersion)
		}
	}
}

configurations {
	create("remapped") {
		canBeConsumed = true
		canBeResolved = false
		attributes {
			attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
			attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
		}
	}
}

artifacts {
	remapped project.tasks.named('jar').map { it.archiveFile }
}

// Note: The compat modules are not published as standalone artifacts, but are shaded into the final plugin jar.
